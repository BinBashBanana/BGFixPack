//ISSUES-BanditScalpPickPocketProblems.tph
PRINT ~Processing ISSUES-BanditScalpPickPocketProblems.tph~
//have scalp created by dialog for those who turn hostile via dialog options
<<<<<<<< scalp.dialog.d
REPLACE_ACTION_TEXT ~teynga~ ~Enemy()~ ~GiveItemCreate("Misc86",Myself,0,0,0)
Enemy()~

REPLACE_ACTION_TEXT ~teynga~ ~ActionOverride("JEMBY",GiveItemCreate("Misc86",Myself,0,0,0)
Enemy())~ ~ActionOverride("JEMBY",GiveItemCreate("Misc86",Myself,0,0,0))
ActionOverride("JEMBY",Enemy())~
REPLACE_ACTION_TEXT ~teynga~ ~ActionOverride("ZEKAR",GiveItemCreate("Misc86",Myself,0,0,0)
Enemy())~ ~ActionOverride("ZEKAR",GiveItemCreate("Misc86",Myself,0,0,0))
ActionOverride("ZEKAR",Enemy())~

REPLACE_ACTION_TEXT ~nevill~ ~Enemy()~ ~GiveItemCreate("Misc86",Myself,0,0,0)
Enemy()~
REPLACE_ACTION_TEXT ~knott~ ~Enemy()~ ~GiveItemCreate("Misc86",Myself,0,0,0)
Enemy()~
REPLACE_ACTION_TEXT ~arghai~ ~Enemy()~ ~GiveItemCreate("Misc86",Myself,0,0,0)
Enemy()~

REPLACE_ACTION_TEXT ~canto~ ~Enemy()~ ~GiveItemCreate("Misc86",Myself,0,0,0)
Enemy()~

REPLACE_ACTION_TEXT ~canto~ ~ActionOverride("DRIBBEN",GiveItemCreate("Misc86",Myself,0,0,0)
Enemy())~ ~ActionOverride("DRIBBEN",GiveItemCreate("Misc86",Myself,0,0,0))
ActionOverride("DRIBBEN",Enemy())~
REPLACE_ACTION_TEXT ~canto~ ~ActionOverride("BILLY",GiveItemCreate("Misc86",Myself,0,0,0)
Enemy())~ ~ActionOverride("BILLY",GiveItemCreate("Misc86",Myself,0,0,0))
ActionOverride("BILLY",Enemy())~
>>>>>>>>
COMPILE ~scalp.dialog.d~
//have scalp created by script for those who turn hostile via hearing a shout or being attacked
<<<<<<<< scalp.script.baf
IF
	Allegiance(Myself,ENEMY)
	!HasItem("misc86",Myself)
THEN
	RESPONSE #100
		 GiveItemCreate("Misc86",Myself,0,0,0)
END
IF
        Die()
	!HasItem("misc86",Myself)
THEN
	RESPONSE #100
		 GiveItemCreate("Misc86",Myself,0,0,0)
END
>>>>>>>>
COPY_EXISTING ~shout.bcs~ ~override/abgfbdsh.bcs~
EXTEND_BOTTOM ~abgfbdsh.bcs~ ~scalp.script.baf~
//these all have the shout.bcs -- apply the modified version in its place. if scalp wasn't created during dialog EA turning, then it is created here. if it wasn't created while hostile and the script catches that they are dead scalp should be created then (blue circle deaths)
ACTION_FOR_EACH ~file~ IN ~TEYNGA.CRE~ ~JEMBY.CRE~ ~NEVILL.CRE~ ~KNOTT.CRE~ ~CANTO.CRE~ ~BILLY.CRE~ ~DRIBBE.CRE~ ~BANDCAP.CRE~
                          ~BANDCR.CRE~ ~BANDIC.CRE~ ~BANDIT.CRE~ ~BANDIT2.CRE~ ~BANDIT3.CRE~ ~BANDITA.CRE~ ~BANDITC.CRE~ ~BANDITCY.CRE~ ~HOBGOBC.CRE~
                          ~RAEMON.CRE~ ~REPMAN.CRE~ ~THUG.CRE~ ~TRALIT.CRE~ BEGIN
 ACTION_IF (FILE_EXISTS_IN_GAME ~%file%~) THEN BEGIN
 COPY_EXISTING_REGEXP ~%file%~ ~override~
  PATCH_IF (%SOURCE_SIZE% > 0x2d4) BEGIN
   FOR (script=0x248;script<0x270;script+=8) BEGIN
    READ_ASCII %script% script_name
    PATCH_IF (~%script_name%~ STRING_EQUAL_CASE ~shout~) BEGIN
     WRITE_ASCII %script% ~abgfbdsh~
     SET %script% = %SOURCE_SIZE%
    END
   END
  END
 BUT_ONLY
 END
END

//arghai.cre has no shout.bcs script  appened the script to the exisiting override script
ACTION_IF (FILE_EXISTS_IN_GAME ~argha.bcs~) THEN BEGIN
 EXTEND_BOTTOM ~argha.bcs~ ~scalp.script.baf~
END

//zekar.cre has no shout.bcs & only has generic fighting script -- override slot is free assign script there -- zekar should shout for help if attacked too
ACTION_IF (FILE_EXISTS_IN_GAME ~ZEKAR.CRE~) THEN BEGIN
 COPY_EXISTING_REGEXP ~zekar.cre~ ~override~
  PATCH_IF (%SOURCE_SIZE% > 0x2d4) BEGIN
   WRITE_ASCII 0x248 ~abgfbdsh~
  END
 BUT_ONLY
END

//remove existing scalp
ACTION_FOR_EACH ~file~ IN ~ARGHAI.CRE~ ~TEYNGA.CRE~ ~ZEKAR.CRE~ ~JEMBY.CRE~ ~NEVILL.CRE~ ~KNOTT.CRE~ ~CANTO.CRE~ ~BILLY.CRE~ ~DRIBBE.CRE~ ~BANDCAP.CRE~
                          ~BANDCR.CRE~ ~BANDIC.CRE~ ~BANDIT.CRE~ ~BANDIT2.CRE~ ~BANDIT3.CRE~ ~BANDITA.CRE~ ~BANDITC.CRE~ ~BANDITCY.CRE~ ~HOBGOBC.CRE~
                          ~RAEMON.CRE~ ~REPMAN.CRE~ ~THUG.CRE~ ~TRALIT.CRE~ BEGIN
 ACTION_IF (FILE_EXISTS_IN_GAME ~%file%~) THEN BEGIN
 COPY_EXISTING_REGEXP ~%file%~ ~override~
  REMOVE_CRE_ITEM ~misc86~
 BUT_ONLY
 END
END
