//ISSUES-BanditScalpPickPocketProblems.tph
PRINT ~Processing ISSUES-BanditScalpPickPocketProblems.tph~
SILENT
//have scalp created by dialog for those who turn hostile via dialog options
<<<<<<<< scalp.dialog.d
REPLACE_ACTION_TEXT ~teynga~ ~Enemy()~ ~GiveItemCreate("Misc86",Myself,0,0,0)
Enemy()~

REPLACE_ACTION_TEXT ~teynga~ ~ActionOverride("JEMBY",GiveItemCreate("Misc86",Myself,0,0,0)
Enemy())~ ~ActionOverride("JEMBY",GiveItemCreate("Misc86",Myself,0,0,0))
ActionOverride("JEMBY",Enemy())~
REPLACE_ACTION_TEXT ~teynga~ ~ActionOverride("ZEKAR",GiveItemCreate("Misc86",Myself,0,0,0)
Enemy())~ ~ActionOverride("ZEKAR",GiveItemCreate("Misc86",Myself,0,0,0))
ActionOverride("ZEKAR",Enemy())~

REPLACE_ACTION_TEXT ~nevill~ ~Enemy()~ ~GiveItemCreate("Misc86",Myself,0,0,0)
Enemy()~
REPLACE_ACTION_TEXT ~knott~ ~Enemy()~ ~GiveItemCreate("Misc86",Myself,0,0,0)
Enemy()~
REPLACE_ACTION_TEXT ~arghai~ ~Enemy()~ ~GiveItemCreate("Misc86",Myself,0,0,0)
Enemy()~

REPLACE_ACTION_TEXT ~canto~ ~Enemy()~ ~GiveItemCreate("Misc86",Myself,0,0,0)
Enemy()~

REPLACE_ACTION_TEXT ~canto~ ~ActionOverride("DRIBBEN",GiveItemCreate("Misc86",Myself,0,0,0)
Enemy())~ ~ActionOverride("DRIBBEN",GiveItemCreate("Misc86",Myself,0,0,0))
ActionOverride("DRIBBEN",Enemy())~
REPLACE_ACTION_TEXT ~canto~ ~ActionOverride("BILLY",GiveItemCreate("Misc86",Myself,0,0,0)
Enemy())~ ~ActionOverride("BILLY",GiveItemCreate("Misc86",Myself,0,0,0))
ActionOverride("BILLY",Enemy())~
>>>>>>>>
COMPILE ~scalp.dialog.d~

//have scalp created by script for those who turn hostile via hearing a shout or being attacked
<<<<<<<< scalp.script.baf
IF
	Allegiance(Myself,ENEMY)
	Specifics(Myself,BANDIT_WITH_SCALP)
	!HasItem("misc86",Myself)
THEN
	RESPONSE #100
		 GiveItemCreate("Misc86",Myself,0,0,0)
END
IF
        Die()
	!HasItem("misc86",Myself)
	Specifics(Myself,BANDIT_WITH_SCALP)
THEN
	RESPONSE #100
		 GiveItemCreate("Misc86",Myself,0,0,0)
END
>>>>>>>>
EXTEND_BOTTOM ~shout.bcs~ ~scalp.script.baf~

//arghai & zekar don't have the shout script.  give it to them.  shouldn't hurt for them to have it.
ACTION_FOR_EACH file IN ~arghai.cre~ ~zekar.cre~ BEGIN
 ACTION_IF (FILE_EXISTS_IN_GAME ~%file%~) THEN BEGIN
 COPY_EXISTING_REGEXP ~%file%~ ~override~
  PATCH_IF (%SOURCE_SIZE% > 0x2d4) BEGIN
   FOR (script=0x248;script<0x270;script+=8) BEGIN
    READ_ASCII %script% script_name
    PATCH_IF !(FILE_EXISTS_IN_GAME ~%script_name%~) BEGIN  //if its a non-existant or empty slot
     WRITE_ASCIIT %script% ~shout~
     SET script = 0x270
    END
   END
  END
 BUT_ONLY
 END
END

//remove existing scalp
ACTION_FOR_EACH ~file~ IN ~ARGHAI.CRE~ ~TEYNGA.CRE~ ~ZEKAR.CRE~ ~JEMBY.CRE~ ~NEVILL.CRE~ ~KNOTT.CRE~ ~CANTO.CRE~ ~BILLY.CRE~ ~DRIBBE.CRE~ ~BANDCAP.CRE~
                          ~BANDCR.CRE~ ~BANDIC.CRE~ ~BANDIT.CRE~ ~BANDIT2.CRE~ ~BANDIT3.CRE~ ~BANDITA.CRE~ ~BANDITC.CRE~ ~BANDITCY.CRE~ ~HOBGOBC.CRE~
                          ~RAEMON.CRE~ ~REPMAN.CRE~ ~THUG.CRE~ ~TRALIT.CRE~ BEGIN
 ACTION_IF (FILE_EXISTS_IN_GAME ~%file%~) THEN BEGIN
 COPY_EXISTING_REGEXP ~%file%~ ~override~
  WRITE_BYTE 0x274 11 //BANDIT_WITH_SCALP
  REMOVE_CRE_ITEM ~misc86~
 BUT_ONLY
 END
END
