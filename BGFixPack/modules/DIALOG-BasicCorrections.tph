//DIALOG-BasicCorrections.tph
PRINT ~Processing DIALOG-BasicCorrections.tph~
// ----------------------------------------------------------------------------
//Ordulinian Quest Reward Fix
<<<<<<<< inlined/orduli.d
REPLACE_ACTION_TEXT ~orduli~ ~GiveItem("CLCK06",LastTalkedToBy)~ ~GiveItemCreate("CLCK06",LastTalkedToBy(),1,0,0)~
>>>>>>>>
COMPILE ~inlined/orduli.d~
// ----------------------------------------------------------------------------
// ----------------------------------------------------------------------------
//Tremain Belde'ar Dialog Crash Fix
<<<<<<<< inlined/tremai.d
REPLACE ~tremai~
IF ~~ THEN BEGIN 6
SAY #12651
IF ~~ THEN EXTERN ~VARCI~ 9
END
END
>>>>>>>>
COMPILE ~inlined/tremai.d~
// ----------------------------------------------------------------------------
//Killing Marl XP Exploit Fix
COMPILE ~BGFixpack/files/IdobeksFixpackFiles/marl.d~
// ----------------------------------------------------------------------------
//Three Old Kegs Noblewoman Exploit Fix
<<<<<<<< inlined/nobw8.d
REPLACE_TRANS_ACTION ~nobw8~ BEGIN 19 20 END BEGIN 0 END ~GiveItem("MISC20",LastTalkedToBy)~ ~GiveItemCreate("MISC20",LastTalkedToBy())~
REPLACE_TRANS_ACTION ~nobw8~ BEGIN 19 20 END BEGIN 0 END ~Global("AcceptedJob","GLOBAL",2)~ ~SetGlobal("AcceptedJob","GLOBAL",2)~
>>>>>>>>
COMPILE ~inlined/nobw8.d~
// ----------------------------------------------------------------------------
//Taerom Fuirium Ankheg Shell Exploit Fix
// being lazy -- used idobeks code & file
COMPILE ~BGFixpack/files/IdobeksFixpackFiles/taerom.d~
// ----------------------------------------------------------------------------
// The Revenant spams dialog during combat -- set some variables to prevent it..
// non-dudley & non-baldurdash
<<<<<<<< revent3change.d
ADD_STATE_TRIGGER ~reven3~ 0 ~Global("ab_BGF_RevenantHelp","GLOBAL",0)~
ADD_TRANS_ACTION ~reven3~ BEGIN 0 END BEGIN END ~SetGlobal("ab_BGF_RevenantHelp","GLOBAL",1)~
ADD_STATE_TRIGGER ~reven3~ 1 ~Global("HelpHentold","GLOBAL",1)~
ADD_TRANS_ACTION ~reven3~ BEGIN 1 END BEGIN END ~SetGlobal("HelpHentold","GLOBAL",2)~
>>>>>>>>
COMPILE ~revent3change.d~
// ----------------------------------------------------------------------------
// The man with the golden pants doesn't give the tip to 18 charisma or better -- now he does
<<<<<<<< inlined/nobl9.d
//following line makes him give the tip to high charisma gabbers
REPLACE_ACTION_TEXT ~nobl9~ ~GivePartyGold(2)~ ~GiveGoldForce(2)~
//add triggers to certain transactions
ADD_TRANS_TRIGGER ~nobl9~ 0 ~HasItem("MISC47",Myself)~ DO 2 3
>>>>>>>>
COMPILE ~inlined/nobl9.d~
// ----------------------------------------------------------------------------
// BGT makes this change which causes Tazok to change allegiance and attack the player's party
<<<<<<<< sarevok.d
REPLACE_ACTION_TEXT ~SAREVO~ ~ ActionOverride("Galdor",Enemy())
ActionOverride("Semaj",Enemy())~ ~ ActionOverride("Galdor",Enemy())
ActionOverride("Tazok",Enemy())
ActionOverride("Semaj",Enemy())~
>>>>>>>>
COMPILE ~sarevok.d~
// ----------------------------------------------------------------------------
// Ithtyl's fix for when she is charmed
// ----------------------------------------------------------------------------
<<<<<<<< ithtyl.d
REPLACE_TRIGGER_TEXT ithtyl ~True();~ ~True()~ //10
REPLACE_TRIGGER_TEXT ITHTYL ~StateCheck(Myself,STATE_CHARMED)~ ~StateCheck(Myself,8192)
CheckStatGT(Myself,19,45)~ //1
>>>>>>>>
COMPILE ~ithtyl.d~
// ----------------------------------------------------------------------------
// Dialog Corrections
// ----------------------------------------------------------------------------
<<<<<<<< inlined/dlg-changes.d
//other misc errors
REPLACE_ACTION_TEXT ~cythan~ ~ActionOverride("ARGHH",DestroySelf())
ActionOverride("ARGHH",DestroySelf())~ ~ActionOverride("ARGHH",DestroySelf())
ActionOverride("UGHH",DestroySelf())~
REPLACE_TRIGGER_TEXT ~fergus~ ~Global("FERGUS1","GLOBAL",2);~ ~Global("FERGUS1","GLOBAL",2)~
REPLACE_TRIGGER_TEXT ~flam7~ ~StateCheck(Myself,STATE_CHARMED);~ ~StateCheck(Myself,STATE_CHARMED)~
//other misc errors as reported by NI
REPLACE_ACTION_TEXT abela ~GiveItemCreate("MISC68",LastTalkedToBy)~ ~GiveItemCreate("MISC68",LastTalkedToBy(),0,0,0)~
REPLACE_ACTION_TEXT brevli ~GiveItemCreate("RING23",LastTalkedToBy)~ ~GiveItemCreate("RING23",LastTalkedToBy(),0,0,0)~
REPLACE_ACTION_TEXT deltan ~GiveItemCreate("MISC55",LastTalkedToBy)~ ~GiveItemCreate("MISC55",LastTalkedToBy(),0,0,0)~
REPLACE_ACTION_TEXT deltan2 ~GiveItemCreate("MISC55",LastTalkedToBy)~ ~GiveItemCreate("MISC55",LastTalkedToBy(),0,0,0)~
REPLACE_ACTION_TEXT deltan2 ~GiveItemCreate("SCRL2R",LastTalkedToBy)~ ~GiveItemCreate("SCRL2R",LastTalkedToBy(),0,0,0)~
REPLACE_ACTION_TEXT narlen ~GiveItemCreate("MISC36",LastTalkedToBy)~ ~GiveItemCreate("MISC36",LastTalkedToBy(),0,0,0)~
REPLACE_ACTION_TEXT quinn ~GiveItemCreate("MISC32",LastTalkedToBy)~ ~GiveItemCreate("MISC32",LastTalkedToBy(),0,0,0)~
REPLACE_ACTION_TEXT tersus ~GiveItemCreate("LEAT01",Player1)~ ~GiveItemCreate("LEAT01",Player1,0,0,0)~
REPLACE_ACTION_TEXT tersus ~GiveItemCreate("LEAT01",Player2)~ ~GiveItemCreate("LEAT01",Player2,0,0,0)~
REPLACE_ACTION_TEXT tersus ~GiveItemCreate("LEAT01",Player3)~ ~GiveItemCreate("LEAT01",Player3,0,0,0)~
REPLACE_ACTION_TEXT tersus ~GiveItemCreate("LEAT01",Player4)~ ~GiveItemCreate("LEAT01",Player4,0,0,0)~
REPLACE_ACTION_TEXT tersus ~GiveItemCreate("LEAT01",Player5)~ ~GiveItemCreate("LEAT01",Player5,0,0,0)~
REPLACE_ACTION_TEXT tersus ~GiveItemCreate("LEAT01",Player6)~ ~GiveItemCreate("LEAT01",Player6,0,0,0)~
//the below are warnings as reported by NI
REPLACE_TRIGGER_TEXT alatos ~"Oberon"~ ~"Oberan"~ //13
REPLACE_ACTION_TEXT beland ~OpenDoor("DR0600")~ ~OpenDoor("Door0600")~
REPLACE_TRIGGER_TEXT bjahei ~"Kahlid"~ ~"Khalid"~ //5, 8
REPLACE_ACTION_TEXT garric ~"Garrickp"~ ~"Garrip"~ //13
REPLACE_ACTION_TEXT jaheij ~"KhalidP"~ ~"KhaliP"~ //1
REPLACE_ACTION_TEXT melica ~"MISC49,~ ~"MISC49",~ //16
REPLACE_TRIGGER_TEXT ~minsc~ ~!Dead("Dynahe")~ ~!Dead("Dynaheir")~
REPLACE_ACTION_TEXT sharte ~"SHARTEEL,~ ~"SHARTEEL",~ //5
REPLACE_TRIGGER_TEXT therel ~Dead("Daltan")~ ~Dead("Dalton")~
REPLACE_ACTION_TEXT viconi ~"viconiap"~ ~"viconp"~ //13
REPLACE_TRIGGER_TEXT volo ~"Serevok"~ ~"Sarevok"~ //13
//Vanilla & ToTSC
REPLACE_TRIGGER_TEXT amnise ~StateCheck(Myself,STATE_CHAMRED)~ ~StateCheck(Myself,STATE_CHARMED)~ //3 V&T
REPLACE_TRIGGER_TEXT bandic ~"Bandcap"4~ ~"Bandcap",4~ //0
REPLACE_ACTION_TEXT ~cattac~ ~Attack(NearestEnemyOf(Myself)~ ~Attack(NearestEnemyOf(Myself))~
REPLACE_TRIGGER_TEXT delorn ~StateCheck(Myself,STATE_DHARMED)~ ~StateCheck(Myself,STATE_CHARMED)~ //6
REPLACE_ACTION_TEXT ~delthy~ ~Global("AskedDelthyr3","GLOABL",1)~ ~SetGlobal("AskedDelthyr3","GLOBAL",1)~
REPLACE_ACTION_TEXT delthy ~"GLOABL"~ ~"GLOBAL"~ //17
REPLACE_TRIGGER_TEXT delthy ~"GLOABL"~ ~"GLOBAL"~
REPLACE_ACTION_TEXT doomsay ~^Global("doomsayer","GLOBAL",0)~ ~SetGlobal("doomsayer","GLOBAL",0)~ //4
REPLACE_ACTION_TEXT drunk ~Sleep()~ ~PlayDead(300)~ //remove bad action and replace with play dead for one game hour
REPLACE_ACTION_TEXT dynahe ~SetDialogue~ ~SetDialog~ //31
REPLACE_ACTION_TEXT dynahe ~"Dynahep"~ ~"Dynahp"~ //31
REPLACE_ACTION_TEXT ~edwinj~ ~GiveGoldCreate(500)~ ~GiveGoldForce(500)~
REPLACE_ACTION_TEXT eldotp ~(Skie"~ ~("Skie"~ //8
REPLACE_TRANS_ACTION ~emerso~ BEGIN 4 END BEGIN 0 END ~Global("EmersonPermission","GLOBAL",1)~ ~SetGlobal("EmersonPermission","GLOBAL",1)~
REPLACE_TRIGGER_TEXT friend ~CheckState(Myself,STATE_CHARMED)~ ~StateCheck(Myself,STATE_CHARMED)~ //6
REPLACE_TRANS_ACTION ~iron12~ BEGIN 4 END BEGIN 0 END ~EscapeArea~ ~EscapeArea()~
REPLACE_TRIGGER_TEXT jared ~SetGlobal("HelpJared","GLOBAL",0)~ ~Global("HelpJared","GLOBAL",0)~ //2, 3, 4
REPLACE_TRIGGER_TEXT ~jared~ ~ReactionGLT(LastTalkedToBy,NEUTRAL_UPPER)~ ~ReactionGT(LastTalkedToBy(),NEUTRAL_UPPER)~
REPLACE_TRIGGER_TEXT ~lahl~ ~Random(~ ~RandomNum(~
REPLACE_TRIGGER_TEXT ~minern~ ~Global("SpokenToEmerson",0)~ ~Global("SpokenToEmerson","GLOBAL",0)~
REPLACE_TRIGGER_TEXT MTOB6 ~SetGlobal("Warning","GLOBAL",1)~ ~Global("Warning","GLOBAL",1)~ //5
REPLACE_TRIGGER_TEXT ~nobl10~ ~ReactionGT(HOSTILE_UPPER)~ ~ReactionGT(LastTalkedToBy(),HOSTILE_UPPER)~
REPLACE_TRIGGER_TEXT ~nobl10~ ~ReactionLT(NEUTRAL_LOWER)~ ~ReactionLT(LastTalkedToBy(),NEUTRAL_LOWER)~
REPLACE_ACTION_TEXT pheirk ~True()~ ~~ //6
REPLACE_ACTION_TEXT pheirkas ~True()~ ~~ //6
REPLACE_TRIGGER_TEXT raiken ~SetGlobal("JoinedBandits","GLOBAL",0)~ ~Global("JoinedBandits","GLOBAL",0)~ //0
REPLACE_TRIGGER_TEXT ~read4~ ~NumberOfTimesTalkedToGT(0)~ ~NumTimesTalkedToGT(0)~
REPLACE_ACTION_TEXT safana ~"Safanap"~ ~"Safanp"~ //16
REPLACE_ACTION_TEXT safana ~SetDialogue~ ~SetDialog~
REPLACE_ACTION_TEXT shoal ~ActionOverride("Droth",MoveToPoint(\[2900.525\])~ ~ActionOverride("Droth",MoveToPoint([2900.525]))~ //6, 7 error on no-TotSC
REPLACE_ACTION_TEXT sonner ~^Global(HelpJebadoh","GLOBAL",2)~ ~SetGlobal("HelpJebadoh","GLOBAL",2)~ //8
REPLACE_TRIGGER_TEXT tamoko ~FRIENLY_LOWER~ ~FRIENDLY_LOWER~ //23
REPLACE_TRIGGER_TEXT teven ~SetGlobal("JoinedBandits","GLOBAL",0)~ ~Global("JoinedBandits","GLOBAL",0)~ //0
REPLACE_ACTION_TEXT test2 ~Attack(\[0\],0,\[0\])~ ~~
REPLACE_ACTION_TEXT rashad ~Die()~ ~~
REPLACE_ACTION_TEXT messed ~Die()~ ~~
REPLACE_ACTION_TEXT nobl5 ~Dialog(\[PC\])~ ~~
>>>>>>>>
COMPILE ~inlined/dlg-changes.d~
