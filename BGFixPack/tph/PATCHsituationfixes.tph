//situationfixes.tph -- patches which in order to resolve something require correcting multiple files
// ----------------------------------------------------------------------------
//Aldeth Sashenstar Betrayal Fix
// i've shamelessly taken Idobek's version and used it instead
ACTION_IF (GAME_IS ~bg1~) THEN BEGIN
COMPILE ~BGFixpack/files/IdobeksFixpackFiles/VanillaRemakes/aldeth.d~
        ~BGFixpack/files/IdobeksFixpackFiles/VanillaRemakes/flamal.baf~
END
ACTION_IF (GAME_IS ~totsc~) THEN BEGIN
COMPILE ~BGFixpack/files/IdobeksFixpackFiles/aldeth.d~
        ~BGFixpack/files/IdobeksFixpackFiles/flamal.baf~
END
EXTEND_TOP ~ar0128.bcs~ ~BGFixpack/files/IdobeksFixpackFiles/ar0128.baf~
EXTEND_TOP ~ar1100.bcs~ ~BGFixpack/files/IdobeksFixpackFiles/ar1100.baf~
COPY ~BGFixpack/files/IdobeksFixpackFiles/flamal.cre~ ~override~
COPY_EXISTING ~dabron.bcs~ ~override~
 DECOMPILE_BCS_TO_BAF
  REPLACE_TEXTUALLY EXACT_MATCH ~See([PC])~ ~See([PC])
NumTimesTalkedTo(0)~
 COMPILE_BAF_TO_BCS
BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ~aldeth.cre~ ~override~
  READ_LONG 0x2bc "itemsoffset"
  READ_LONG 0x2c0 "#items"
  WHILE ("%#items%" > 0) BEGIN
    SET "item" = ("%itemsoffset%" + ("%#items%" - 1) * 0x14)
    READ_LONG ("%item%" + 0x00) "itemresref1"
    READ_LONG ("%item%" + 0x04) "itemresref2"
    READ_BYTE ("%item%" + 0x10) "flags"
    WHILE ("%itemresref1%" = 0x48315753)
      AND ("%itemresref2%" = 0x00003330)
      AND NOT (("%flags%" BAND 0b00000010) = 0b00000010) BEGIN
        WRITE_BYTE ("%item%" + 0x10) ("%flags%" BOR 0b00000010)
    READ_BYTE ("%item%" + 0x10) "flags"
    END
  SET "#items" = ("%#items%" - 1)
  END
BUT_ONLY_IF_IT_CHANGES
// ----------------------------------------------------------------------------
//Gorpel Hind Summons Many Maulers Fix
<<<<<<<< inlined/gorpel.d
REPLACE_TRIGGER_TEXT ~gorpel~ ~NumTimesTalkedTo()~ ~NumTimesTalkedTo(0)~
REPLACE_ACTION_TEXT gretek ~ActionOverride("PARGUS",Enemy())~ ~~
REPLACE_ACTION_TEXT gretek ~ActionOverride("WILF",Enemy())~ ~~
REPLACE_ACTION_TEXT gretek ~ActionOverride("NADER",Enemy())~ ~~
REPLACE_ACTION_TEXT gretek ~ActionOverride("ARLIN",Enemy())~ ~~
REPLACE_ACTION_TEXT gretek ~ActionOverride("CATURAK",Enemy())~ ~SetGlobal("ab_bgf_gretekmad","GLOBAL",1)~
REPLACE_ACTION_TEXT gretek ~Shout(99)~ ~~
>>>>>>>>
COMPILE ~inlined/gorpel.d~
<<<<<<<< make_enemy.baf
IF
Global("ab_bgf_gretekmad","GLOBAL",1)
!Global("ab_bgf_i_am_mad","LOCALS",1)
THEN
RESPONSE #100
SetGlobal("ab_bgf_i_am_mad","LOCALS",1)
Enemy()
END
>>>>>>>>
EXTEND_BOTTOM ~abgfight.bcs~ ~make_enemy.baf~
ACTION_FOR_EACH file0 IN ~gretek~ ~pargus~ ~wilf~ ~nader~ ~arlin~ ~catura~ BEGIN
 COPY_EXISTING ~%file0%.cre~ ~override~
//get rid of offending shout script and add this one instead...
  WRITE_ASCII_TERMINATE 0x248 ~abgfight~ //reassign override script to our value
 BUT_ONLY_IF_IT_CHANGES
END
// ----------------------------------------------------------------------------
//Jalantha Mistmyr Pickpocket Exploit Fix
<<<<<<<< jalant/stuffcheck.baf
IF
!Global("BOYBODY","GLOBAL",1)
Dead("Jalantha")
PartyHasItem("misc54")
THEN
RESPONSE #100
SetGlobal("BOYBODY","GLOBAL",1)
END
>>>>>>>>
//add it to the area script or create an area script if it's not there
COPY_EXISTING ~ar0609.are~ ~override~
 READ_ASCII 0x94 ~area_script~
 INNER_ACTION BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~%area_script%.bcs~) THEN BEGIN
   EXTEND_BOTTOM ~%area_script%.bcs~ ~jalant/stuffcheck.baf~
  END ELSE BEGIN
   OUTER_SET assignit = 1
   EXTEND_BOTTOM ~ar0609.bcs~ ~jalant/stuffcheck.baf~
  END
 END
 PATCH_IF (%assignit%=1) BEGIN
  WRITE_ASCII_TERMINATE 0x94 ~ar0609~
 END
BUT_ONLY_IF_IT_CHANGES
// flag the item as unstealable in the cre file so that it can't be taken
COPY_EXISTING ~jalant.cre~ ~override~
 READ_LONG 700 ~itm_loc~
 READ_LONG 704 ~itm_num~
 FOR (y = 0; y < %itm_num%; y = y + 1) BEGIN
  READ_ASCII %itm_loc% ~itm_name~
  PATCH_IF (~MISC54~ STRING_EQUAL_CASE ~%itm_name%~) BEGIN
   READ_LONG (%itm_loc% + 16) ~flag~
   PATCH_IF !(%flag% = 2) BEGIN
    WRITE_LONG (%itm_loc% + 16) 2
   END
  END
  SET itm_loc = (%itm_loc% + 20)
 END
BUT_ONLY_IF_IT_CHANGES
//block dialog states depending on status of body
<<<<<<<< tenya2.d
ADD_STATE_TRIGGER tenya2 0 ~GlobalLT("BOYBODY","GLOBAL",1)~
ADD_STATE_TRIGGER tenya2 6 ~GlobalLT("BOYBODY","GLOBAL",1)~
REPLACE_STATE_TRIGGER tenya2 11 ~Global("BOYBODY","GLOBAL",1)~
ADD_TRANS_ACTION tenya2 BEGIN 11 END BEGIN END ~RandomWalk()~
>>>>>>>>
COMPILE ~tenya2.d~
COPY_EXISTING ~tenya2.cre~ ~override~
 WRITE_ASCII_TERMINATE 0x248 ~shout~ //override
BUT_ONLY_IF_IT_CHANGES
// ----------------------------------------------------------------------------
//Ramazith Pickpocket Exploit Fix
// Prevent multiple pickpockets of Ramazith's pockets -- but you can still pick his pockets
<<<<<<<< inlined/ramazi.d
//These attempt to false the states so that they don't show again.
ADD_TRANS_TRIGGER ramazi 7 ~False()~ DO 2
ADD_TRANS_TRIGGER ramazi 8 ~False()~ DO 0
>>>>>>>>
COMPILE ~inlined/ramazi.d~
<<<<<<<< ramazi/stuffcheck.baf
//stole is set if taken by pick pocket
IF
Exists("Ramazith")
!Global("ab_BGF_bothgone","GLOBAL",1) //not destroyed
!HasItem("ring07","Ramazith") //stolen
!HasItem("amul16","Ramazith") //stolen
THEN
RESPONSE #100
SetGlobal("ab_BGF_bothstole","GLOBAL",1) //to destroy
END

IF
Exists("Ramazith")
!Global("ab_BGF_amulgone","GLOBAL",1) //not destroyed
HasItem("ring07","Ramazith") //still got
!HasItem("amul16","Ramazith") //stolen
THEN
RESPONSE #100
SetGlobal("ab_BGF_amulstole","GLOBAL",1) //to destroy
END

IF
Exists("Ramazith")
!Global("ab_BGF_ringgone","GLOBAL",1) //not destroyed
HasItem("amul16","Ramazith") //still got
!HasItem("ring07","Ramazith") //stolen
THEN
RESPONSE #100
SetGlobal("ab_BGF_ringstole","GLOBAL",1) //to destroy
END

//gone is set if destroyed because was stolen
IF
Exists("Ramazith")
Global("ab_BGF_bothstole","GLOBAL",1) //to destroy
HasItem("ring07","Ramazith") //has but shouldn't
HasItem("amul16","Ramazith") //has but shouldn't
THEN
RESPONSE #100
ActionOverride("Ramazith",DestroyItem("amul16")) //destroy
ActionOverride("Ramazith",DestroyItem("ring07")) //destroy
SetGlobal("ab_BGF_bothgone","GLOBAL",1) //told gone
END

IF
Exists("Ramazith")
Global("ab_BGF_amulstole","GLOBAL",1) //to destroy
HasItem("amul16","Ramazith") //has but shouldn't
THEN
RESPONSE #100
ActionOverride("Ramazith",DestroyItem("amul16")) //destroy
SetGlobal("ab_BGF_amulgone","GLOBAL",1) //told gone
END

IF
Exists("Ramazith")
Global("ab_BGF_ringstole","GLOBAL",1) //to destroy
HasItem("ring07","Ramazith") //has but shouldn't
THEN
RESPONSE #100
ActionOverride("Ramazith",DestroyItem("ring07")) //destroy
SetGlobal("ab_BGF_ringgone","GLOBAL",1) //told gone
END
>>>>>>>>
COPY_EXISTING ~ramazi.cre~ ~override~
 READ_ASCII 0x248 ~or_script~ //find override script
 INNER_ACTION BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~%or_script%.bcs~) THEN BEGIN //see if it's real
   COPY_EXISTING ~%or_script%.bcs~ ~override/abgframi.bcs~ //copy it and give it our name
   EXTEND_BOTTOM ~abgframi.bcs~ ~ramazi/stuffcheck.baf~ //add our stuff
  END ELSE BEGIN //otherwise
   EXTEND_BOTTOM ~abgframi.bcs~ ~ramazi/stuffcheck.baf~ //make new when add our stuff fails
  END
 END
 WRITE_ASCII_TERMINATE 0x248 ~abgframi~ //reassign override script to our value
BUT_ONLY_IF_IT_CHANGES
// ----------------------------------------------------------------------------
//Oublek Emerald Bounty Exploit Fix
// An attempt to fix Oublek and the Bounty exploits
<<<<<<<< inlined/oublek.d
//OublekBounty1 is for brage
//OublekBounty2 is for prism
ADD_TRANS_TRIGGER ~oublek~ 9 ~Global("OublekBounty1","GLOBAL",0)~ DO 0
ADD_TRANS_TRIGGER ~oublek~ 9 ~Global("OublekBounty2","GLOBAL",0)~ DO 1
ADD_TRANS_TRIGGER ~oublek~ 9 ~Global("OublekBounty1","GLOBAL",0)
Global("OublekBounty2","GLOBAL",0)~ DO 4
ADD_TRANS_TRIGGER ~oublek~ 10 ~Global("OublekBounty1","GLOBAL",0)~ DO 1 5
ADD_TRANS_TRIGGER ~oublek~ 10 ~Global("OublekBounty2","GLOBAL",0)~ DO 9 5
>>>>>>>>
COMPILE ~inlined/oublek.d~
ACTION_IF !(GAME_IS ~bg1~) THEN BEGIN
//put this in a library file at some point if it works
OUTER_INNER_PATCH ~12~ BEGIN
  WRITE_BYTE 1 0x09
  READ_ASCII 1 tab (1)  // 0x09, tab
  WRITE_BYTE 1 0x0a
  READ_ASCII 1 lnl (1)  // 0x0a, Linux
  WRITE_BYTE 0 0x0d
  READ_ASCII 0 mnl (1)  // 0x0d, Mac
  READ_ASCII 0 wnl (2)  // 0x0d0a, Windows
END
APPEND ~trigger.ids~ ~0x407A NumItemsParty(S:ResRef*,I:Num*)~
              UNLESS ~0x407A[ %tab%]+NumItemsParty(S:ResRef\*,I:Num\*)~
APPEND ~trigger.ids~ ~0x407B NumItemsPartyGT(S:ResRef*,I:Num*)~
              UNLESS ~0x407B[ %tab%]+NumItemsPartyGT(S:ResRef\*,I:Num\*)~
APPEND ~trigger.ids~ ~0x407C NumItemsPartyLT(S:ResRef*,I:Num*)~
              UNLESS ~0x407C[ %tab%]+NumItemsPartyLT(S:ResRef\*,I:Num\*)~
<<<<<<<< inlined/oublek.d
ADD_TRANS_TRIGGER ~oublek~ 9 ~!NumItemsParty("misc43",2)~ DO 4
ADD_TRANS_TRIGGER ~oublek~ 10 ~!NumItemsParty("misc43",2)~ DO 5
REPLACE_TRANS_TRIGGER oublek BEGIN 9 END BEGIN 1 END ~PartyHasItem("MISC43")~ ~NumItemsParty("MISC43",2)~
REPLACE_TRANS_TRIGGER oublek BEGIN 10 END BEGIN 9 END ~PartyHasItem("MISC43")~ ~NumItemsParty("MISC43",2)~
>>>>>>>>
COMPILE ~inlined/oublek.d~
END
// ----------------------------------------------------------------------------
// Fuller will tell where Hull's sword is located -- This is a restoration
// dudley - similar restoration
<<<<<<<< .../d-inlined/dlg-changes.d
REPLACE_TRIGGER_TEXT ~fuller~ ~PartyHasItem("")~ ~!PartyHasItem("SW1H12")
Global("HelpHull","GLOBAL",1)~

REPLACE_ACTION_TEXT ~hull~ ~SetGlobal("HelpHull","GLOBAL",1)~ ~SetGlobal("HelpHull","GLOBAL",2)~
ADD_TRANS_ACTION ~hull~ BEGIN 0 END BEGIN END ~SetGlobal("HelpHull","GLOBAL",1)~
SET_WEIGHT ~hull~ 2 #3
REPLACE_STATE_TRIGGER ~hull~ 2 ~Global("HelpHull","GLOBAL",1)~
>>>>>>>>
COMPILE ~.../d-inlined/dlg-changes.d~
// ----------------------------------------------------------------------------
// The Nobleman and Noblewoman in the backroom main floor of Winthrop's Inn will now banter with the player -- restoration
// dudley - similar restoration BUT this uses existing text
<<<<<<<< .../d-inlined/dlg-changes.d
ADD_STATE_TRIGGER ~nobw4~ 5 ~!StateCheck(Myself,STATE_CHARMED)~
ADD_STATE_TRIGGER ~nobl4~ 8 ~NumTimesTalkedToGT(0)
ReactionLT(LastTalkedToBy(),FRIENDLY_LOWER)~
REPLACE_STATE_TRIGGER ~nobl4~ 9 ~NumTimesTalkedToGT(0)
ReactionGT(LastTalkedToBy(),NEUTRAL_UPPER)~
ADD_TRANS_ACTION ~nobl4~ BEGIN 7 END BEGIN END ~GiveGoldForce(20)~
SET_WEIGHT ~nobl4~ 7 #0
SET_WEIGHT ~nobl4~ 8 #1
SET_WEIGHT ~nobl4~ 9 #2
SET_WEIGHT ~nobl4~ 0 #3
>>>>>>>>
COMPILE ~.../d-inlined/dlg-changes.d~
// ----------------------------------------------------------------------------
// Prevent the Yeslick kickout loop
//~Fix Yeslick kickout looping issue~
COPY_EXISTING ~yeslick.bcs~ ~override~
  DECOMPILE_BCS_TO_BAF
   REPLACE_TEXTUALLY EXACT_MATCH ~GlobalTimerExpired("Yeslick","GLOBAL")~ ~GlobalTimerExpired("Yeslick","GLOBAL") !Global("MineFlood","GLOBAL",2)~
  COMPILE_BAF_TO_BCS
BUT_ONLY_IF_IT_CHANGES
<<<<<<<< .../d-inlined/dlg-changes.d
REPLACE_TRIGGER_TEXT ~yeslij~ ~False()~ ~GlobalTimerExpired("Yeslick","GLOBAL")
Global("MineFlood","GLOBAL",1)~
ADD_TRANS_ACTION ~yeslij~ BEGIN 3 END BEGIN END ~SetGlobal("MineFlood","GLOBAL",2)~
>>>>>>>>
COMPILE ~.../d-inlined/dlg-changes.d~
// ----------------------------------------------------------------------------
// Monks will appear and speak if you leave the building without killing them -- now they won't
// dudley - DV assignment only
COPY_EXISTING ~carbos.cre~ ~override~   // Carbos (Candlekeep, Tutorial)
              ~shank.cre~ ~override~    // Shank (Candlekeep, Tutorial)
 PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
  WRITE_EVALUATED_ASCII 0x280 ~%SOURCE_RES%~ #18 // dv = 18 bytes
 END
BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ~ar2600.bcs~ ~override~
 DECOMPILE_BCS_TO_BAF
  REPLACE_TEXTUALLY EXACT_MATCH ~Global("carbos","GLOBAL",1)~ ~Global("carbos","GLOBAL",1)
Dead("shank")~
  REPLACE_TEXTUALLY EXACT_MATCH ~Global("shank","GLOBAL",1)~ ~Global("shank","GLOBAL",1)
Dead("carbos")~
 COMPILE_BAF_TO_BCS
BUT_ONLY_IF_IT_CHANGES
// ----------------------------------------------------------------------------
// Some instances of the rats not getting counted -- change to a more reliable method
// non-dudley & non-baldurdash
COPY_EXISTING ~rat2.cre~ ~override~
 PATCH_IF (%SOURCE_SIZE% > 724) BEGIN
  WRITE_ASCII 0x280 ~ab_bgf_rats~
  WRITE_ASCII 0x250 ~~ (8)
 END
BUT_ONLY_IF_IT_CHANGES
<<<<<<<< inlined/ar2606.baf
IF
NumDeadGT("ab_bgf_rats",4)
THEN
RESPONSE #100
SetGlobal("RatsKilled","GLOBAL",5)
END
>>>>>>>>
COMPILE ~inlined/ar2606.baf~
COPY_EXISTING ~ar2606.are~ ~override~
 PATCH_IF (%SOURCE_SIZE% > 284) BEGIN
  WRITE_ASCII 0x94 ~ar2606~
 END
BUT_ONLY_IF_IT_CHANGES
// ----------------------------------------------------------------------------
// Gullykin temple no longer shares stock with Nashkell temple
// ----------------------------------------------------------------------------
ACTION_IF !(FILE_EXISTS_IN_GAME ~TEM4003.sto~) THEN BEGIN // if Gullykin does not have a store already
 COPY_EXISTING ~TEM4802.sto~ ~override/TEM4003.sto~ // clones and renames Temple of Helm
  WRITE_LONG 0x0C 20713 //assign the text string directly rather than the words.
 BUT_ONLY_IF_IT_CHANGES
END
<<<<<<<< alvan.d
REPLACE_ACTION_TEXT alvahe
  ~StartStore("Tem4802",LastTalkedToBy())~
  ~StartStore("TEM4003",LastTalkedToBy())~
>>>>>>>>
COMPILE ~alvan.d~
// ----------------------------------------------------------------------------
// Lothander doesn't loop & speaks up - Marek can be charmed or pickpocketed to solve quest
// ----------------------------------------------------------------------------
//~Fixes Lothander loop at Blade and Stars~
<<<<<<<< .../d-inlined/dlg-changes.d
REPLACE_TRIGGER_TEXT lothan ~SetGlobal(~ ~Global(~ //0,10
>>>>>>>>
COMPILE ~.../d-inlined/dlg-changes.d~
ACTION_IF !(GAME_IS ~totsc~) THEN BEGIN
<<<<<<<< .../d-inlined/dlg-changes.d
REPLACE_TRANS_ACTION lothan BEGIN 14 END BEGIN 0 END ~Global("LothanderMove","GLOBAL",5)~ ~SetGlobal("LothanderMove","GLOBAL",5)~
>>>>>>>>
COMPILE ~.../d-inlined/dlg-changes.d~
END
//~Allow Lothander to initiate dialog after seeing diviner~
//allows Lothander to initate dialog when PC leaves the diviners tent
//many people forget to talk to him here
<<<<<<<< inlined/abgfinit.baf
IF
  See([PC])
THEN
  RESPONSE #100
    Dialogue([PC])
END
>>>>>>>>
COMPILE ~inlined/abgfinit.baf~
//this area extension changes Lothanders class script to the one above
<<<<<<<< inlined/ab_ar0700.baf
IF
  Global("LothanderMove","GLOBAL",4)
THEN
  RESPONSE #100
    ActionOverride("Lothander",ChangeAiScript("abgfinit",CLASS))
END
>>>>>>>>
EXTEND_BOTTOM ~ar0700.bcs~ ~inlined/ab_ar0700.baf~
//~Marek does what he says when charmed: gives antidote~
//adjusted things to be identical with UB's  Changes are the same code is a little different.
<<<<<<<< .../d-inlined/dlg-changes.d
REPLACE_ACTION_TEXT marek ~DisplayString("Marek",20675)~ ~DisplayString(Player1,20675)~
ADD_STATE_TRIGGER ~marek~ 5 ~Global("PartyCured","GLOBAL",0)~
ADD_TRANS_ACTION ~marek~ BEGIN 5 END BEGIN END ~GiveItem("POTN47",LastTalkedToBy())
SetGlobal("PartyCured","GLOBAL",1)
DisplayString(Player1,20675)
AddExperienceParty(10000)~
>>>>>>>>
COMPILE ~.../d-inlined/dlg-changes.d~
//~Solve party poison quest if antidote was pickpocketed from Marek~
//this area extension solves party poisoned quest IF the antidote was pickpocketed from Marek
<<<<<<<< inlined/ab_ar0115.baf
IF
  Global("PartyCured","GLOBAL",0)
  Exists("Marek")  // ~Marek~
  !Dead("Marek")  // ~Marek~
  PartyHasItem("POTN47")
THEN
  RESPONSE #100
    SetGlobal("PartyCured","GLOBAL",1)
    DisplayString("Marek",20675)
    AddExperienceParty(10000)
END
>>>>>>>>
EXTEND_BOTTOM ~ar0115.bcs~ ~inlined/ab_ar0115.baf~
ACTION_IF (GAME_IS ~totsc~) THEN BEGIN
// ----------------------------------------------------------------------------
//Ulgoth's Beard Residents Hostile When Cult Attacks Fix
 COPY_EXISTING ~cult3.cre~ ~override~
  WRITE_ASCIIT 0x250 ~cultsht~
 BUT_ONLY_IF_IT_CHANGES
<<<<<<<< cult3.d
REPLACE_ACTION_TEXT ~cult3~ ~ActionOverride("Cult Wizard",MoveToPoint(\[2900.904\]))~ ~ActionOverride("Cult Wizard 1",MoveToPoint([2900.904]))~
REPLACE_ACTION_TEXT ~cult3~ ~ActionOverride("Cult Archer",MoveToPoint(\[2716.877\]))~ ~ActionOverride("cult fodder",MoveToPoint([2716.877]))~
>>>>>>>>
 COMPILE ~cult3.d~
// ----------------------------------------------------------------------------
//Dudley patch -- Counting House, AR0307 and AR0308 -
// ----------------------------------------------------------------------------
//both area scripts conflict with the dialogue of Ulf and Kieres respectively such that if you end up in a fight with Ulf,
//he triggers an action that tries to turn the guards hostile (who are upstairs from him) and if you fight with Kieres,
//he turns the crewmates hostile (who are downstairs from him).
//It would seem most likely that the guards should guard the lower floor and the crewmates are with their captain.
//If there is any doubt the captain calls to his "mates" for help.
ACTION_FOR_EACH file IN ~crew1~ ~crew2~ ~crew3~ ~crew4~ ~crew5~ ~crew6~ ~crew7~ BEGIN
 COPY_EXISTING ~%file%.cre~ ~override~
  WRITE_ASCII 0x280 ~~ (32) //removed old
  WRITE_ASCIIE 0x280 ~%SOURCE_RES%~ (5)  //assign new
 BUT_ONLY_IF_IT_CHANGES
END
COPY_EXISTING ar0308.bcs override
 DECOMPILE_BCS_TO_BAF
  REPLACE_TEXTUALLY EXACT_MATCH ~LEAGGU~ ~CREW~
 COMPILE_BAF_TO_BCS
BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ar0307.bcs override
 DECOMPILE_BCS_TO_BAF
  REPLACE_TEXTUALLY EXACT_MATCH ~CREW~ ~LEAGGU~
 COMPILE_BAF_TO_BCS
BUT_ONLY_IF_IT_CHANGES
END
