// ----------------------------------------------------------------------------
// Name:    BGFixPack
// Author:  Sasha al'Therin (aka plainab) (aka you ain't getting my real name LOL)
// Date:    Ongoing
// Version: 0.0014
// ----------------------------------------------------------------------------
BACKUP ~BGFixPack/backup~
AUTHOR ~~

ASK_EVERY_COMPONENT
VERSION ~0.0014~

LANGUAGE ~English~ ~English~ ~BGFixPack/languages/English/setup.tra~

BEGIN @1 //~Required for all Components/patches~
INCLUDE ~BGFixPack\tph\winxp-totsc-glitchy-override.tph~
INCLUDE ~BGFixPack\tph\mac-fixes-by-devsin.tph~

// ----------------------------------------------------------------------------
//GAME TEXT UPDATE
BEGIN @2 //~Game Text Update AND File Adjustments to Match~
REQUIRE_COMPONENT ~Setup-BGFixPack.tp2~ ~0~ @3 //~SKIPPING Required Component not installed~
REQUIRE_PREDICATE (~%LANGUAGE%~ STRING_COMPARE_CASE ~English~ =0) @4 //~English language version required to install GTU~
ACTION_IF (GAME_IS ~bg1~) THEN BEGIN
 INCLUDE ~bgfixpack/tph/gtu_vanilla.tph~
END
ACTION_IF (GAME_IS ~totsc~) THEN BEGIN
 INCLUDE ~bgfixpack/tph/gtu_totsc.tph~
END
//the following are dependent upon the new gtu.
COPY_EXISTING ~misc55.itm~ ~override~ //match new description -- makes sense
 PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
  WRITE_LONG 0x4c 175 // weight
 END
BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ~potn46.itm~ ~override~  //match new description -- makes sense
              ~potn47.itm~ ~override~  //match new description -- makes sense
              ~potn48.itm~ ~override~  //match new description -- makes sense
              ~dagg04.itm~ ~override~ //match other daggers & description change -- makes sense
 PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
  WRITE_LONG 0x4c 1 // weight
 END
BUT_ONLY_IF_IT_CHANGES
ACTION_IF (GAME_IS ~totsc~) THEN BEGIN
 COPY_EXISTING ~misc65.itm~ ~override~ //assign id name to general and modify description
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
   WRITE_LONG 0x8 15843 //general name
   WRITE_LONG 0x4c 185 // weight
  END
 BUT_ONLY_IF_IT_CHANGES
 COPY_EXISTING ~misc67.itm~ ~override~ //assign id name to general and modify description
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
   WRITE_LONG 0x8 16324 // unid name
  END
 BUT_ONLY_IF_IT_CHANGES
 COPY_EXISTING ~misc68.itm~ ~override~ //assign general to id name
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
   WRITE_LONG 0xc 16624 // id name
  END
 BUT_ONLY_IF_IT_CHANGES
END


